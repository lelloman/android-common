apply plugin: "com.github.ben-manes.versions"

buildscript {

    ext {
        androidx_test_core_version = "1.2.0"
        androidx_test_rules_version = "1.2.0"
        androidx_test_runner_version = "1.2.0"
        app_compat_version = "1.0.2"
        arch_core_testing_version = "2.0.1"
        assert_j_version = "3.14.0"
        constraint_layout_version = "1.1.3"
        dagger_version = "2.23.2"
        dex_opener_version = "2.0.4"
        espresso_version = "3.2.0"
        koin_version = '2.0.1'
        kotlin_version = '1.3.61'
        lifecycle_version = "2.0.0"
        material_design_version = "1.0.0"
        mime_craft_version = "1.1.1"
        mockito_kotlin_version = "2.1.0"
        mockito_android_version = "3.1.0"
        okhttp_version = "4.2.2"
        picasso_version = "2.71828"
        recycler_view_version = "1.0.0"
        room_version = "1.1.1"
        rx_android_version = "2.1.1"
        rx_java_version = "2.2.15"
        truth_version = "1.0"
        uiautomator_version = "2.1.3"
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath "com.github.ben-manes:gradle-versions-plugin:0.27.0"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url "https://jitpack.io"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

subprojects { Project project ->

    boolean isApplication = project.getProperties()["isApplication"]
    if (isApplication) {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    println("Configure '${project.name}' " + (isApplication ? "application" : "library"))
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion 29
        defaultConfig {
            minSdkVersion 16
            targetSdkVersion 29
            versionCode 1
            versionName "1.0"
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        compileOptions {
            sourceCompatibility 1.8
            targetCompatibility 1.8
        }

        lintOptions {
            lintConfig file(rootDir.absolutePath + "/lint.xml")
        }
    }
}